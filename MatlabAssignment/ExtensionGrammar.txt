<PROG> ::= "BEGIN" "{" <INSTRCLIST>

<INSTRCLIST> ::= "}" | <INSTRC> <INSTRCLIST>
<INSTRC> ::= <PRINT> | <SET> | <CREATE> | <LOOP>

# Print array or one-word string to stdout
<PRINT> ::= "PRINT" <VARNAME> | "PRINT" <STRING>

# One of the 26 possible (upper-case) variables
<VARNAME> ::= "$"[A-Z]     # e.g. $A, $B, $Z etc.

# Because of the assumption that a program is just a list of words,
strings can't have spaces in them (for simplicity)
<STRING> :: Double-quoted string e.g. "../../doof.arr", "Hello!" etc.

<SET> ::= "SET" <VARNAME> ":=" <POLISHLIST>
<POLISHLIST> ::= <POLISH><POLISHLIST>  | ";"

<POLISH> ::= <PUSHDOWN> | <UNARYOP> | <BINARYOP> | <INDEX>
<PUSHDOWN> ::== <VARNAME> | <INTEGER>

# A non-negative integer
<INTEGER> ::= [0-9]+    # e.g. 1, 250,  3

# Pop one array, push the result.
# U-NOT : Flip the Boolean values of an array
# U-EIGHTCOUNT : Returns the numbers of true values around each cell in the array in its
# Moore 8-neighbourhood (north, south, west, east, NE, NW, SW, SE).
<UNARYOP> ::= "U-NOT" | "U-EIGHTCOUNT"

# Pop 2 arrays, push the resultant array
# If both arrays are bigger than 1x1, they must be the same size
# If one array is a 1x1 scalar, apply this value to each cell of the other array in turn
# B-TIMES operates on corresponding cells in turn (it is not a full matrix multiplication).
<BINARYOP> ::= "B-AND" | "B-OR" | "B-GREATER" | "B-LESS" | "B-ADD" | "B-TIMES" | "B-EQUALS"

# Access a subset of another variable
# Can be a single value, multiple values or a range of values
<INDEX> ::= <VARNAME> "[" <INDEX_RANGE> "]"
<INDEX_RANGE> ::= <RANGE> | <ROW_RANGE> "," <COL_RANGE>

# Ranges are used for Indexing and for Removal
# There is a special case of ranges, above, for indexing alone
# Row and Column numbering start from 1, in line with MATLAB's implementation
# Ranges are not required to be in order (i.e. [ 2 : 1 , : ] equivalent to [ 1 : 2 , : ] )
<RANGE> ::= <ROW_RANGE> "," <ALL> | <ALL> "," <COL_RANGE> | <ALL> "," <ALL>
<ROW_RANGE> ::= <INTEGER> | <INTEGER> ":" <INTEGER>
<COL_RANGE> ::= <INTEGER> | <INTEGER> ":" <INTEGER>
<ALL> ::= ":"

# Create an array full of ones, or read from a file
<CREATE> ::= "ONES" <ROWS> <COLS> <VARNAME> | "READ" <FILENAME> <VARNAME> | "RAND" <ROWS> <COLS> <MAX> <VARNAME>
<ROWS> ::= <INTEGER>
<COLS> ::= <INTEGER>
<MAX> ::= <INTEGER>
<FILENAME> ::= <STRING>

# Remove variables entirely, or remove rows or columns individually or by range
# Setting ALL for both range values has the equivalent functionality as removing a variable
<REMOVE> ::= "RMV" <VARNAME> | "RMV" <VARNAME> "[" <RANGE> "]"

# Loop using a variable to count from 1 (!) to <= <INTEGER>
# IF the variable
<LOOP> ::= "LOOP" <VARNAME> <INTEGER> "{" <INSTRCLIST>